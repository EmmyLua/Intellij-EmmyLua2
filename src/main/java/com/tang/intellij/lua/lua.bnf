/*
 * Copyright (c) 2017. tangzx(love.tangzx@qq.com)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

{
  parserClass="com.tang.intellij.lua.parser.LuaParser"

  implements="com.tang.intellij.lua.psi.LuaPsiElement"
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Lua"
  psiImplClassSuffix="Impl"
  psiPackage="com.tang.intellij.lua.psi"
  psiImplPackage="com.tang.intellij.lua.psi.impl"
  psiImplUtilClass="com.tang.intellij.lua.psi.LuaPsiImplUtilKt"

  parserUtilClass="com.tang.intellij.lua.psi.LuaParserUtil"
  elementTypeHolderClass="com.tang.intellij.lua.psi.LuaTypes"
  elementTypeClass="com.tang.intellij.lua.psi.LuaElementType"
  elementTypeFactory="com.tang.intellij.lua.lang.LuaParserDefinitionKt.createType"
  tokenTypeClass="com.tang.intellij.lua.psi.LuaTokenType"
  tokenTypeFactory="com.tang.intellij.lua.lang.LuaParserDefinitionKt.createToken"
  parserImports = "static com.intellij.lang.WhitespacesBinders.*"

  tokens = [
        /* Keywords */
        AND = "and"
        BREAK = "break"
        DO = "do"
        ELSE = "else"
        ELSEIF = "elseif"
        END = "end"
        FALSE = "false"
        FOR = "for"
        FUNCTION = "function"
        IF = "if"
        IN = "in"
        LOCAL = "local"
        NIL = "nil"
        NOT = "not"
        OR = "or"
        REPEAT = "repeat"
        RETURN = "return"
        THEN = "then"
        TRUE = "true"
        UNTIL = "until"
        WHILE = "while"
        REGION = "REGION"
        ENDREGION = "ENDREGION"

        SHEBANG = "#!"
        SHEBANG_CONTENT = "SHEBANG_CONTENT"
        ELLIPSIS = "..."
        CONCAT = ".."
        EQ = "=="
        GE = ">="
        LE = "<="
        NE = "~="
        MINUS = "-"
        PLUS = "+"
        MULT = "*"
        MOD = "%"
        DIV = "/"
        ASSIGN = "="
        GT = ">"
        LT = "<"
        LPAREN = "("
        RPAREN = ")"
        LBRACK = "["
        RBRACK = "]"
        LCURLY = "{"
        RCURLY = "}"
        GETN = "#"
        COMMA = ","
        SEMI = ";"
        COLON = ":"
        DOT = "."
        EXP = "^"

        SHORT_COMMENT="SHORT_COMMENT"
        DOC_COMMENT="DOC_COMMENT"
        BLOCK_COMMENT="BLOCK_COMMENT"
        ID="regexp:[A-Za-z_][A-Za-z0-9_]*"
        NUMBER="NUMBER"
        STRING = "STRING"

        //lua 5.3
        DOUBLE_COLON = "::"
        GOTO = "goto"
        BIT_OR = "|"
        BIT_AND = "&"
        BIT_LTLT = "<<"
        BIT_RTRT = ">>"
        BIT_TILDE = "~"
        DOUBLE_DIV = "//"

        // 非标准符号
        LOGICAL_NOT = "!"
        LOGICAL_OR = "||"
        LOGICAL_AND = "&&"
        NOT_EQ = "!="
        BACKTICK = "`"
        CONTINUE = 'continue'

        // 复合赋值操作符
        PLUS_ASSIGN = "+="
        MINUS_ASSIGN = "-="
        MULT_ASSIGN = "*="
        DIV_ASSIGN = "/="
        MOD_ASSIGN = "%="
        EXP_ASSIGN = "^="
        DOUBLE_DIV_ASSIGN = "//="
        BIT_OR_ASSIGN = "|="
        BIT_AND_ASSIGN = "&="
        BIT_LTLT_ASSIGN = "<<="
        BIT_RTRT_ASSIGN = ">>="

  ]

  hooks("localDef|assignStat|tableField") = [leftBinder="MY_LEFT_COMMENT_BINDER" rightBinder="MY_RIGHT_COMMENT_BINDER"]
  hooks("funcDef|localFuncDef|classMethodDef") = [leftBinder="MY_LEFT_COMMENT_BINDER"]
  hooks("fieldList") = [leftBinder="GREEDY_LEFT_BINDER" rightBinder="GREEDY_RIGHT_BINDER"]
  extends(".*Expr") = expr
  extends(".*Stat") = statement
  hooks(".*Stat") = [leftBinder="MY_LEFT_COMMENT_BINDER"]
}

luaFile ::= shebang_line? stat_semi*

shebang_line ::= SHEBANG SHEBANG_CONTENT

private stat_semi ::= <<parseStatement>>
block ::= stat_semi* {
    implements = [
    ]
}

fake statement ::= () {

}
private stat_impl ::=
    emptyStat |
    doStat |
    whileStat |
    repeatStat |
    ifStat |
    forAStat |
    forBStat |
    defStat |
    lastStat |
    labelStat |
    gotoStat |
    assignStat |
    compoundAssignStat |
    exprStat {
        recoverWhile = stat_recover
    }
private stat_recover ::= !(ID
    | ',' | ';'
    | 'local' | 'do' | 'while' | 'repeat' | 'function' | 'if' | 'for' | 'return' | break
    | nil | true | false | STRING | NUMBER | '::' | 'goto'
    | unaryOp)

emptyStat ::= ';'

varList ::= varExpr (',' varExpr)* {
    extends = exprList
}
assignStat ::= varList '=' exprList {
    pin = 2
    implements = [
    ]
    methods = [assign = 'ASSIGN' varExprList = '/exprList[0]' valueExprList = '/exprList[1]']
}

compoundAssignStat ::= varExpr compoundAssignOp expr {
    pin = 2
    implements = [
    ]
    methods = [compoundAssign = '/compoundAssignOp[0]' varExpr = '/varExpr[0]' valueExpr = '/expr[0]']
}
doStat ::= 'do' <<lazyBlock>> 'end' {
    pin = 1
}
whileStat ::= 'while' expr 'do' <<lazyBlock>> 'end' {
    pin = 1
}
repeatStat ::= 'repeat' <<lazyBlock>> 'until' expr {
    pin = 1
    methods = [
        until = 'UNTIL'
    ]
}
ifStat ::= 'if' expr 'then' <<lazyBlock>> elseifClause* elseClause? 'end' {
    pin = 1
}

elseifClause ::= 'elseif' expr 'then' <<lazyBlock>> {
    pin = 1
}

elseClause ::= 'else' <<lazyBlock>> {
    pin = 1
}

forAStat ::= 'for' paramNameDef '=' expr ',' expr (',' expr)? 'do' <<lazyBlock>> 'end' {
    pin = 3
}
forBStat ::= 'for' parList 'in' exprList 'do' <<lazyBlock>> 'end' {
    pin = 1
}
labelStat ::= '::' ID '::' {
    pin = 1
}
gotoStat ::= 'goto' ID {
    pin = 1
    methods = [
        getReferences
    ]
}
private defStat ::= classMethodDef | funcDef | localFuncDef | localDef
exprStat ::= expr {
}

funcDef ::= 'function' ID funcBody {
    pin = 1
}

private checkFuncPrefix ::= ID ('.' | ':') ID

left funcPrefixRef ::= '.' <<repeat checkFuncPrefix 1>> ID { extends = indexExpr elementType = indexExpr }
classMethodName ::=  nameExpr (funcPrefixRef)* (('.' ID) | (':' ID)) {
    pin = 1
    methods = [
        dot = 'DOT'
        colon = 'COLON'
    ]
}
classMethodDef ::= 'function' classMethodName funcBody {
    pin = 2
}

localFuncDef ::= 'local' 'function' ID funcBody {
    pin = 2
}
attribute ::= '<' ID '>' {
    pin = 1
}
nameList ::= nameDef attribute? (',' nameDef attribute?)* {
}
localDef ::= 'local' nameList ('=' exprList)? {
    pin = 1
}

returnStat ::= return exprList? {
}
breakStat ::= break
continueStat ::= continue
private lastStat ::= returnStat | breakStat | continueStat

exprList ::= expr (',' expr)* {
}

literalExpr ::= nil | false | true | NUMBER | STRING | "..." {
}
stringExpr ::= STRING {
    extends = literalExpr
    elementType = literalExpr
}

private varExpr ::= primaryExpr

expr ::= <<parseExpr>> {
}

parenExpr ::= '(' expr ')' {
    pin = 1
}
unaryExpr ::= unaryOp (unaryExpr | primaryExpr) {
    pin = 1
}

left binaryExpr ::= binaryOp expr {
    pin = 1
}

left indexExpr ::= '[' expr ']' | '.' ID | ':' ID {
    pin(".*") = 1
}

args ::= listArgs | singleArg {
//    stubClass = 'com.tang.intellij.lua.stubs.LuaPlaceholderStub'
}
singleArg ::= tableExpr | stringExpr { extends = args }
private arg_expr_list ::= (expr ',')* (expr |& ')') { pin = 1 }
listArgs ::= '(' (arg_expr_list)? ')' { extends = args pin = 1 }

left callExpr ::= args {
}

private suffixExpr ::= indexExpr | callExpr
private prefixExpr ::= parenExpr | nameExpr | tableExpr | literalExpr
// prefixExpr | callExpr | indexExpr
primaryExpr ::= prefixExpr (suffixExpr*)

closureExpr ::= 'function' funcBody {
    pin = 1
}

funcBody ::= '(' (parList)? ')' <<lazyBlock>>? 'end' {
    pin = 1
}

private parList ::= paramNameDef (',' paramNameDef)* (',' '...')? | '...' { recoverWhile = parList_recover }
private parList_recover ::= !(')'|'in')

tableExpr ::= '{' fieldList '}' {
    pin = 1
}

private fieldList ::= (tableField (tableFieldSep tableField)* (tableFieldSep)?)?

private tableField1 ::= '[' expr ']' '=' expr { pin = 1 }
private tableField2 ::= ID '=' expr { pin = 2 }

tableField ::= tableField1 | tableField2 | expr {
    recoverWhile = tableField_recover
}

private tableField_recover ::= !(tableFieldSep | '}' | '[')

tableFieldSep ::= ',' | ';'

binaryOp ::= '+' | '-' | '*' | '/' | '^' | '%' | '..' |
    '<' | '<=' | '>' | '>=' | '==' | '~=' |
    'and' | 'or'
    // lua5.3
    | '|' | '&' | '>>' | '<<' | '~' | '//'
    // 非标准符号
    | '!=' | '||' | '&&'

assignOp ::= '='

compoundAssignOp ::= '+=' | '-=' | '*=' | '/=' | '%=' | '^=' | '//=' |
    '|=' | '&=' | '<<=' | '>>='

unaryOp ::= '-' | 'not' | '#'
    // lua5.3
    | '~'
    // 非标准符号
    | '!'
paramNameDef ::= ID {
    extends = nameDef
}

nameDef ::= ID {
}
nameExpr ::= ID {
}
